#######################################################################################################################
;	Драйвер работы с адресными светодиодными лентами.
;	разработал Николаев Сергей (г. Кулебаки).
;	Все права защищены.
#######################################################################################################################

#include "mcu.h"
#include "addr_leds_driver.h"

######################################################################################################
# 1. Функция обновления состояния светодиодов  
######################################################################################################

addr_leds_colors_apply:
	# Сохранение контекста верхнего уровня
	push r16
	push r17
	push r18
	push r19
	in r18, STATUS_REG_ADDR

	# Сброс выхода
	cbi PORT_REG_ADDR, PIN_NUM

	# Пауза 50 микросекунд перед каждым кадром
	rcall addr_leds_permisives_wait
	 
	# Для перебора Байтов массива используем регистр косвенной адресации 
	movw Z, r22

	# Перебор байтов массива со значениями устанавливаемых цветов светодиодов 
	array_bytes_loop:
		ld r16, Z+
		ldi r17, 8
		
		# Выключаем прерывания
		cli

		# Перебор всех разрядов одного Байта
		bits_of_byte_loop:

			##############################################################################################
			; В случае изменения частоты тактирования МК нужно будет редактировать этот фрагмент кода
			##############################################################################################
			
			# Проверка какое значение у текущего разряда текущего Байта
			sbrs r16, 7 
			rjmp current_bit_is_reset 

			# Если текущий разряд текущего Байта установлен
			current_bit_is_set:
				# Установка выхода
				sbi PORT_REG_ADDR, PIN_NUM
				ldi r19, 3
				delay_8_clock_cycles:
					dec r19
					brne delay_8_clock_cycles
				nop
				# Сброс выхода
				cbi PORT_REG_ADDR, PIN_NUM
				lsl r16
				dec r17
				brne bits_of_byte_loop
				rjmp bits_of_byte_ended

			# Если текущий разряд текущего Байта сброшен
			current_bit_is_reset:
				# Установка выхода
				sbi PORT_REG_ADDR, PIN_NUM
				nop
				nop
				nop
				nop
				nop
				# Сброс выхода
				cbi PORT_REG_ADDR, PIN_NUM

				ldi r19, 2
				delay_5_clock_cycles:
					dec r19
					brne delay_5_clock_cycles

				lsl r16
				dec r17
				brne bits_of_byte_loop

			# У текущего Байта закончились разряды
			bits_of_byte_ended:

			#######################################################################################################

		# Только если изначально прерывания были включены, то включаем их
		sbrc r18, 7
		sei
		
		# Декрементируем счётчик цикла перебора Байтов
		sbiw r24, 1
		# Проверяем не пора ли завершать цикл перебора Байтов
		brne array_bytes_loop
	
	# Установка выхода 
	sbi PORT_REG_ADDR, PIN_NUM

	# Восстановление контекста верхнего уровня
	out STATUS_REG_ADDR, r18
	pop r19
	pop r18
	pop r17
	pop r16
	ret

######################################################################################################
# 2. Функция инициализации 
######################################################################################################

addr_leds_init:
	# Настройка порта вывода
	sbi PORT_DIRECTION_REG_ADDR, PIN_NUM

	# Сброс выхода
	cbi PORT_REG_ADDR, PIN_NUM

	# Ожидание 50 микросекунд
	rcall addr_leds_permisives_wait

	# Установка выхода
	sbi PORT_REG_ADDR, PIN_NUM

	ret

######################################################################################################
# 3. Функция ожидания разрешения для обновления состояния светодиодов  
######################################################################################################

LOOP_ITERS_COUNT_FOR_50_US = ((F_CPU_MHZ * 50) / 4)

addr_leds_permisives_wait:
	push r26
	push r27

	ldi r26, lo8(LOOP_ITERS_COUNT_FOR_50_US)
	ldi r27, hi8(LOOP_ITERS_COUNT_FOR_50_US)
	wait_50_us:  
		sbiw r26, 1
		brne wait_50_us
	
	pop r27
	pop r26
	ret

##########################################################################################################
